apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 15
    }

    lintOptions {
        abortOnError false
    }
}

task androidJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: androidJavadoc) {
    classifier = 'javadoc'
    from androidJavadoc.destinationDir
}

dependencies {
    compile project(":core")

    compile 'org.mockito:mockito-core:1.9.5'
    compile "com.google.dexmaker:dexmaker:1.1"
    compile "com.google.dexmaker:dexmaker-mockito:1.1"
    compile 'org.roboguice:roboguice:3.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact file("$buildDir/outputs/aar/android-debug.aar")
            artifact sourcesJar
            artifact javadocJar

            artifactId "injected-test-android"

            pom.withXml {
                def root = asNode()
                def dependencies = root.appendNode("dependencies")
                configurations.compile.allDependencies.each {
                    if(it['group'] != null) {
                        def dependency = dependencies.appendNode("dependency")
                        dependency?.appendNode("groupId", it["group"])
                        dependency?.appendNode("artifactId", it["name"])
                        dependency?.appendNode("version", it["version"])
                    }
                }
                root.children().last() + pomConfig
            }
        }
    }
}

bintray.pkg.name = "injected-test-android"