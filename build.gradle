buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group 'com.jeskeshouse'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile 'junit:junit:4.10'
    compile 'org.mockito:mockito-all:1.9.0'
    compile 'com.google.android:android:4.1.1.4'
    compile 'org.roboguice:roboguice:2.0'
    compile('org.robolectric:robolectric:2.3') {
        exclude group: 'com.android.support'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '2.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

def pomConfig = {
    licenses {
        license {
            name 'The MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id "jeske717"
            name "Ben Jeske"
            email "jeske717@gmail.com"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintrayUserName
    key = bintrayApiKey
    publish = true
    publications = ['mavenJava']

    pkg {
        repo = 'android-utils'
        name = 'injected-test-runner'
    }
}