buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
        classpath 'com.android.tools.build:gradle:0.14.0'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:1.12.3'
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}

if (!project.parent) {
    apply plugin: 'release'
    createReleaseTag.dependsOn { subprojects.collect{ it.tasks['bintrayUpload']} }
}

task wrapper(type: Wrapper) {
    gradleVersion '2.1'
}

allprojects {
    group 'com.jeskeshouse'
    version '2.0.1-SNAPSHOT'

    apply plugin: 'maven'
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        jcenter()
    }

    bintray {
        user = bintrayUserName
        key = bintrayApiKey
        publish = true
        publications = ['mavenJava']

        pkg {
            repo = 'android-utils'
        }
    }
}

ext.pomConfig = {
    licenses {
        license {
            name 'The MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id "jeske717"
            name "Ben Jeske"
            email "jeske717@gmail.com"
        }
    }
}