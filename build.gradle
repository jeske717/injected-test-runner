buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

allprojects {
    group 'com.jeskeshouse'
    version '2.0.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'maven'
}

subprojects {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    compileJava {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
    }

    repositories {
        jcenter()
    }

    task wrapper(type: Wrapper) {
        gradleVersion '2.1'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    def pomConfig = {
        licenses {
            license {
                name 'The MIT License'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id "jeske717"
                name "Ben Jeske"
                email "jeske717@gmail.com"
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom.withXml {
                    def root = asNode()
                    root.children().last() + pomConfig
                }
            }
        }
    }

    bintray {
        user = bintrayUserName
        key = bintrayApiKey
        publish = true
        publications = ['mavenJava']

        pkg {
            repo = 'android-utils'
        }
    }
}